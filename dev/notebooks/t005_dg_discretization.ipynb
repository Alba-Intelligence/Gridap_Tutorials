{
 "cells": [
  {
   "outputs": [],
   "cell_type": "markdown",
   "source": [
    "# Tutorial 6: Poisson equation (with DG)\n",
    "\n",
    "\n",
    "## Learning outcomes\n",
    "\n",
    "- How to solve a simple with a Discontinuous Galerkin (DG) discretization\n",
    "- How to build discontinuous FE spaces\n",
    "- How to integrate quantities on the mesh skeleton\n",
    "- How to compute jumps and averages of quantities on the mesh skeleton\n",
    "\n",
    "\n",
    "## Problem statement\n",
    "\n",
    "The goal of this tutorial is to solve a simple PDE using a Discontinuous Galerkin (DG) formulation. For simplicity, we take the Poisson equation on the unit multi dimensional cube $\\Omega \\doteq (0,1)^d$, with $d=2$ and $d=3$, as a model problem:\n",
    "\n",
    "\n",
    "$$\n",
    "\\left\\lbrace\n",
    "\\begin{aligned}\n",
    "-\\Delta u = f  \\ \\text{in} \\ \\Omega\\\\\n",
    "u = g \\ \\text{on}\\ \\partial\\Omega,\\\\\n",
    "\\end{aligned}\n",
    "\\right.\n",
    "$$\n",
    "where $f$ is the source term and $g$ is the Dirichlet boundary value.\n",
    "\n",
    " We are going to solve two version of this problem. In a first stage, we take $d=3$ and consider a manufactured solution, namely $u(x) = 3 x_1 + x_2 + 2 x_3$, that belongs to the FE interpolation that we will build below. In this case, we expect to compute a numerical solution with an approximation error close to the machine precision. On the other hand, we will perform a convergence test for the 2D case ($d=2$). To this, end we will consider a manufactured solution that cannot by represented exactly by the interpolation, namely $u(x)=x_2 \\sin(2 \\pi\\ x_1)$. Our goal is to confirm that the convergence order of the discretization error is the optimal one.\n",
    "\n",
    "## Numerical Scheme\n",
    "\n",
    "In contrast to previous tutorials, we consider a DG formulation to approximate the problem. For the sake of simplicity, we take the well know (symmetric) interior penalty method. For this formulation, the approximation space is made of discontinuous piece-wise polynomials, namely\n",
    "\n",
    "$$\n",
    "V \\doteq \\{ v\\in L^2(\\Omega):\\ v|_{T}\\in Q_p(T) \\text{ for all } T\\in\\mathcal{T}  \\},\n",
    "$$\n",
    "where $\\mathcal{T}$ is the set of all cells $T$ of the FE mesh, and $Q_p(T)$ is a polynomial space of degree $p$ defined on a generic cell $T$. For simplicity, we consider Cartesian meshes in this tutorial. In this case, the space $Q_p(T)$ is made of multi-variate polynomials up to degree $p$ in each spatial coordinate.\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "In order to write the weak form of the problem, we need to introduce the set of interior and boundary facets associated with the FE mesh, denoted here as $\\mathcal{F}_\\Gamma$ and $\\mathcal{F}_{\\partial\\Omega}$ respectively. In addition, for a given function $v\\in V$ restricted to the interior facets $\\mathcal{F}_\\Gamma$, we need to define the well known jump and mean value operators:\n",
    "$$\n",
    "\\lbrack\\!\\lbrack v\\ n \\rbrack\\!\\rbrack \\doteq v^+\\ n^+ + v^- n^-, \\text{ and } \\{\\! \\!\\{\\nabla v\\}\\! \\!\\} \\doteq \\dfrac{ \\nabla v^+ + \\nabla v^-}{2},\n",
    "$$\n",
    "with $v^+$, and $v^-$ being the restrictions of $v\\in V$ to the cells $T^+$, $T^-$ that share a generic interior facet in $\\mathcal{F}_\\Gamma$, and $n^+$, and $n^-$ are the facet outward unit normals from either the perspective of $T^+$ and $T^-$ respectively.\n",
    "\n",
    "With this notation, the weak form associated with the interior penalty formulation reads: find $u\\in V$ such that $a(v,u) = b(v)$ for all $v\\in V$. The bilinear $a(\\cdot,\\cdot)$ and linear form $b(\\cdot)$ have contributions associated with the bulk of $\\Omega$, and the boundary and interior facets $\\mathcal{F}_{\\partial\\Omega}$, $\\mathcal{F}_\\Gamma$, namely\n",
    "``` math\n",
    "\\begin{aligned}\n",
    "a(v,u) &= a_{\\Omega}(v,u) + a_{\\partial\\Omega}(v,u) + a_{\\Gamma}(v,u),\\\\\n",
    "b(v) &= b_{\\Omega}(v) + b_{\\partial\\Omega}(v),\n",
    "\\end{aligned}\n",
    "```\n",
    "which are defined as\n",
    "$$\n",
    "a_{\\Omega}(v,u) \\doteq \\sum_{T\\in\\mathcal{T}} \\int_{T} \\nabla v \\cdot \\nabla u \\ {\\rm d}T, \\quad b_{\\Omega}(v) \\doteq \\int_{\\Omega} v\\ f \\ {\\rm d}\\Omega,\n",
    "$$\n",
    "for the volume\n",
    "$$\n",
    "\\begin{aligned}\n",
    "a_{\\partial\\Omega}(v,u) &\\doteq \\sum_{F\\in\\mathcal{F}_{\\partial\\Omega}} \\dfrac{\\gamma}{|F|} \\int_{F} v\\ u \\ {\\rm d}F -  \\sum_{F\\in\\mathcal{F}_{\\partial\\Omega}} \\int_{F} v\\ (\\nabla u \\cdot n)  \\ {\\rm d}F -  \\sum_{F\\in\\mathcal{F}_{\\partial\\Omega}} \\int_{F} (\\nabla v \\cdot n)\\ u  \\ {\\rm d}F, \\\\\n",
    "b_{\\partial\\Omega} &\\doteq \\sum_{F\\in\\mathcal{F}_{\\partial\\Omega}} \\dfrac{\\gamma}{|F|} \\int_{F} v\\ g \\ {\\rm d}F  -  \\sum_{F\\in\\mathcal{F}_{\\partial\\Omega}} \\int_{F} (\\nabla v \\cdot n)\\ g  \\ {\\rm d}F,\n",
    "\\end{aligned}\n",
    "$$\n",
    "for the boundary facets and\n",
    "$$\n",
    "a_{\\Gamma}(v,u) \\doteq \\sum_{F\\in\\mathcal{F}_{\\Gamma}} \\dfrac{\\gamma}{|F|} \\int_{F} \\lbrack\\!\\lbrack v\\ n \\rbrack\\!\\rbrack\\cdot \\lbrack\\!\\lbrack u\\ n \\rbrack\\!\\rbrack \\ {\\rm d}F -  \\sum_{F\\in\\mathcal{F}_{\\Gamma}} \\int_{F} \\lbrack\\!\\lbrack v\\ n \\rbrack\\!\\rbrack\\cdot \\{\\! \\!\\{\\nabla u\\}\\! \\!\\} \\ {\\rm d}F -  \\sum_{F\\in\\mathcal{F}_{\\Gamma}} \\int_{F} \\{\\! \\!\\{\\nabla v\\}\\! \\!\\}\\cdot \\lbrack\\!\\lbrack u\\ n \\rbrack\\!\\rbrack \\ {\\rm d}F,\n",
    "$$\n",
    "for the interior facets. In previous expressions, $|F|$ denotes the diameter of the face $F$ (in our Cartesian grid, this is equivalent to the characteristic mesh size $h$), and $\\gamma$ is a stabilization parameter that should be chosen large enough such that the bilinear form $a(\\cdot,\\cdot)$ is stable and continuous. Here, we take $\\gamma = p\\ (p+1)$.\n",
    "\n",
    "## 3D manufactured solution"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Gridap\n",
    "import Gridap: ∇\n",
    "\n",
    "u(x) = 3*x[1] + x[2] + 2*x[3]\n",
    "∇u(x) = VectorValue(3.0,1.0,2.0)\n",
    "∇(::typeof(u)) = ∇u\n",
    "f(x) = 0.0\n",
    "g(x) = u(x)\n",
    "\n",
    "L = 1.0\n",
    "limits = (0.0, L, 0.0, L, 0.0, L)\n",
    "n = 4\n",
    "model = CartesianDiscreteModel(domain=limits, partition=(n,n,n))\n",
    "\n",
    "h = L / n\n",
    "\n",
    "order = 3\n",
    "\n",
    "fespace = FESpace(\n",
    "  reffe=:Lagrangian,\n",
    "  conformity = :L2,\n",
    "  valuetype = Float64,\n",
    "  model = model,\n",
    "  order = order)\n",
    "\n",
    "γ = order*(order+1)\n",
    "\n",
    "V = TestFESpace(fespace)\n",
    "U = TrialFESpace(fespace)\n",
    "\n",
    "trian = Triangulation(model)\n",
    "quad = CellQuadrature(trian,degree=2*order)\n",
    "\n",
    "btrian = BoundaryTriangulation(model)\n",
    "bquad = CellQuadrature(btrian,degree=2*order)\n",
    "\n",
    "strian = SkeletonTriangulation(model)\n",
    "squad = CellQuadrature(strian,degree=2*order)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "outputs": [],
   "cell_type": "markdown",
   "source": [
    "![](../assets/t006_poisson_dg/skeleton_trian.png)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "nb = NormalVector(btrian)\n",
    "ns = NormalVector(strian)\n",
    "\n",
    "writevtk(strian,\"strian\")\n",
    "\n",
    "a_Ω(v,u) = inner(∇(v), ∇(u))\n",
    "b_Ω(v) = inner(v,f)\n",
    "t_Ω = AffineFETerm(a_Ω,b_Ω,trian,quad)\n",
    "\n",
    "a_∂Ω(v,u) = (γ/h) * inner(v,u) - inner(v, ∇(u)*nb ) - inner(∇(v)*nb, u)\n",
    "b_∂Ω(v) = (γ/h) * inner(v,g) - inner(∇(v)*nb, g)\n",
    "t_∂Ω = AffineFETerm(a_∂Ω,b_∂Ω,btrian,bquad)\n",
    "\n",
    "a_Γ(v,u) = (γ/h) * inner( jump(v*ns), jump(u*ns)) -\n",
    "  inner( jump(v*ns), mean(∇(u)) ) - inner( mean(∇(v)), jump(u*ns) )\n",
    "t_Γ = LinearFETerm(a_Γ,strian,squad)\n",
    "\n",
    "op = LinearFEOperator(V,U,t_Ω,t_∂Ω,t_Γ)\n",
    "\n",
    "uh = solve(op)\n",
    "\n",
    "uh_Γ = restrict(uh,strian)\n",
    "\n",
    "writevtk(strian,\"jumps\",\n",
    " cellfields=[\"jump_u\"=>jump(uh_Γ), \"jump_gradn_u\"=> jump(∇(uh_Γ)*ns)])"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "outputs": [],
   "cell_type": "markdown",
   "source": [
    "![](../assets/t006_poisson_dg/jump_u.png)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "e = u - uh\n",
    "\n",
    "writevtk(trian,\"trian\",cellfields=[\"uh\"=>uh,\"e\"=>e])"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "outputs": [],
   "cell_type": "markdown",
   "source": [
    "![](../assets/t006_poisson_dg/error.png)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "l2(u) = inner(u,u)\n",
    "h1(u) = a_Ω(u,u) + l2(u)\n",
    "\n",
    "el2 = sqrt(sum( integrate(l2(e),trian,quad) ))\n",
    "eh1 = sqrt(sum( integrate(h1(e),trian,quad) ))\n",
    "\n",
    "\n",
    "const k = 2*pi\n",
    "u(x) = sin(k*x[1]) * x[2]\n",
    "∇u(x) = VectorValue(k*cos(k*x[1])*x[2], sin(k*x[1]))\n",
    "f(x) = (k^2)*sin(k*x[1])*x[2]\n",
    "∇(::typeof(u)) = ∇u\n",
    "\n",
    "function run(n,order)\n",
    "\n",
    "  #Setup model\n",
    "  L = 1.0\n",
    "  h = L / n\n",
    "  limits = (0.0, L, 0.0, L)\n",
    "  partition = (n,n)\n",
    "  model = CartesianDiscreteModel(domain=limits, partition=partition)\n",
    "\n",
    "  #Setup FE spaces\n",
    "  fespace = FESpace(\n",
    "    reffe=:Lagrangian,\n",
    "    conformity = :L2,\n",
    "    valuetype = Float64,\n",
    "    model = model,\n",
    "    order = order)\n",
    "  V = TestFESpace(fespace)\n",
    "  U = TrialFESpace(fespace)\n",
    "\n",
    "  #Setup integration meshes\n",
    "  trian = Triangulation(model)\n",
    "  btrian = BoundaryTriangulation(model)\n",
    "  strian = SkeletonTriangulation(model)\n",
    "\n",
    "  #Setup quadratures\n",
    "  quad = CellQuadrature(trian,degree=2*order)\n",
    "  squad = CellQuadrature(strian,degree=2*order)\n",
    "  bquad = CellQuadrature(btrian,degree=2*order)\n",
    "\n",
    "  #Setup normal vectors\n",
    "  nb = NormalVector(btrian)\n",
    "  ns = NormalVector(strian)\n",
    "\n",
    "  #Setup weak form (volume)\n",
    "  a_Ω(v,u) = inner(∇(v), ∇(u))\n",
    "  b_Ω(v) = inner(v,f)\n",
    "\n",
    "  #Setup weak form (boundary)\n",
    "  γ = order*(order+1)\n",
    "  a_∂Ω(v,u) = (γ/h) * inner(v,u) - inner(v, ∇(u)*nb ) - inner(∇(v)*nb, u)\n",
    "  b_∂Ω(v) = (γ/h) * inner(v,g) - inner(∇(v)*nb, g)\n",
    "\n",
    "  #Setup weak form (skeleton)\n",
    "  a_Γ(v,u) = (γ/h) * inner( jump(v*ns), jump(u*ns)) -\n",
    "    inner( jump(v*ns), mean(∇(u)) ) - inner( mean(∇(v)), jump(u*ns) )\n",
    "\n",
    "  #Setup FE problem\n",
    "  t_Ω = AffineFETerm(a_Ω,b_Ω,trian,quad)\n",
    "  t_Γ = LinearFETerm(a_Γ,strian,squad)\n",
    "  t_∂Ω = AffineFETerm(a_∂Ω,b_∂Ω,btrian,bquad)\n",
    "  op = LinearFEOperator(V,U,t_Ω,t_∂Ω,t_Γ)\n",
    "\n",
    "  #Solve\n",
    "  uh = solve(op)\n",
    "\n",
    "  #Measure discretization error\n",
    "  e = u - uh\n",
    "  l2(u) = inner(u,u)\n",
    "  h1(u) = a_Ω(u,u) + l2(u)\n",
    "  el2 = sqrt(sum( integrate(l2(e),trian,quad) ))\n",
    "  eh1 = sqrt(sum( integrate(h1(e),trian,quad) ))\n",
    "\n",
    "  (el2, eh1, h)\n",
    "\n",
    "end\n",
    "\n",
    "function conv_test(ns,order)\n",
    "\n",
    "  el2s = Float64[]\n",
    "  eh1s = Float64[]\n",
    "  hs = Float64[]\n",
    "\n",
    "  for n in ns\n",
    "    @show n\n",
    "    el2, eh1, h = run(n,order)\n",
    "    push!(el2s,el2)\n",
    "    push!(eh1s,eh1)\n",
    "    push!(hs,h)\n",
    "  end\n",
    "\n",
    "  (el2s, eh1s, hs)\n",
    "\n",
    "end\n",
    "\n",
    "\n",
    "el2s, eh1s, hs = conv_test([8,16,32,64],3)\n",
    "\n",
    "using Plots\n",
    "\n",
    "plot(hs,[el2s eh1s],\n",
    "    xaxis=:log, yaxis=:log,\n",
    "    label=[\"L2\" \"H1\"],\n",
    "    shape=:auto,\n",
    "    xlabel=\"h\",ylabel=\"error norm\")\n",
    "\n",
    "\n",
    "function slope(hs,errors)\n",
    "  x = log10.(hs)\n",
    "  y = log10.(errors)\n",
    "  linreg = hcat(fill!(similar(x), 1), x) \\ y\n",
    "  linreg[2]\n",
    "end\n",
    "\n",
    "slope(hs,el2s)\n",
    "\n",
    "slope(hs,eh1s)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "outputs": [],
   "cell_type": "markdown",
   "source": [
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.2.0"
  },
  "kernelspec": {
   "name": "julia-1.2",
   "display_name": "Julia 1.2.0",
   "language": "julia"
  }
 },
 "nbformat": 4
}
